plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

// TMDB API key storage
def props = new Properties()
new File(rootDir, "tmdb.properties").withInputStream { props.load(it) }
def tmdbApiKey = props.getProperty("key")

// Development options
def HTTP_DEBUG = false

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.tajmoti.tulip"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 5
        versionName "0.5.0"
        buildConfigField('String', 'TMDB_API_KEY', "\"$tmdbApiKey\"")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'HTTP_DEBUG', "false"
        }
        debug {
            buildConfigField 'boolean', 'HTTP_DEBUG', "${HTTP_DEBUG}"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    // Android framework
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'

    // DI
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-fragment:1.0.0"

    // DB
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Ktor
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"

    // Retrofit and Moshi
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    // Logging
    implementation "org.slf4j:slf4j-api:$slf4j_version"
    implementation 'com.github.tony19:logback-android:2.0.0'

    // Business logic libs
    implementation project(':libtulip')
    implementation project(':libtvprovider')
    implementation project(':libtvprovider-kinox')
    implementation project(':libtvprovider-primewire')
    implementation project(':libwebdriver-android')
    implementation project(':libwebdriver')
    implementation project(':libtvvideoextractor')
    implementation project(':libtmdb')
}