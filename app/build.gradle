plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

// Development options
def HTTP_DEBUG = false

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.tajmoti.tulip"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 11
        versionName "0.10.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'HTTP_DEBUG', "false"
        }
        debug {
            buildConfigField 'boolean', 'HTTP_DEBUG', "${HTTP_DEBUG}"
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk true
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

secrets {
    propertiesFileName 'secrets.properties'
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url "https://dl.bintray.com/videolan/Android" }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    // Android framework
    def lifecycle_version = '2.4.0-rc01'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"

    // Android miscellaneous
    def groupie_version = '2.9.0'
    implementation "com.github.lisawray.groupie:groupie:$groupie_version"
    implementation "com.github.lisawray.groupie:groupie-viewbinding:$groupie_version"
    implementation "com.github.lisawray.groupie:groupie-databinding:$groupie_version"
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    implementation "org.videolan.android:libvlc-all:3.4.1"

    // DI
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-fragment:1.0.0"

    // DB
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Ktor
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"

    // Retrofit and Moshi
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    // Logging
    implementation "org.slf4j:slf4j-api:$slf4j_version"
    implementation 'com.github.tony19:logback-android:2.0.0'

    // Business logic libs
    implementation project(':libtulip')
    implementation project(':libtvprovider')
    implementation project(':libtvprovider-kinox')
    implementation project(':libtvprovider-primewire')
    implementation project(':libwebdriver-android')
    implementation project(':libwebdriver')
    implementation project(':libtvvideoextractor')
    implementation project(':libtmdb')
    implementation project(':libopensubtitles')
}